plugins {
    id 'java'
}

group 'com.codurance'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

sourceSets {
    acceptance {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir 'src/acceptance/java'
        resources.srcDir 'src/acceptance/resources'
    }
}

configurations {
    acceptanceImplementation.extendsFrom testImplementation
    acceptanceRuntimeOnly.extendsFrom testRuntimeOnly
}

task acceptance(type: Test) {
    description = "Run acceptance tests"
    group = "verification"
    testClassesDirs = sourceSets.acceptance.output.classesDirs
    classpath = sourceSets.acceptance.runtimeClasspath
}

test {
    useJUnitPlatform()
}

acceptance {
    useJUnitPlatform()
}